def modelDir = new File(repoRoot, "rider/protocol/src/main/kotlin/model")
def hashBaseDir = new File(repoRoot, "rider/build/rdgen")

// TODO: Think about adding an msbuild task for rdgen

task generateFrontendBackendModel(type: tasks.getByName('rdgen').class) {
    def backendCsOutput = new File(repoRoot, "resharper/resharper-unity/src/Rider/Model")
    def frontendKtOutput = new File(repoRoot, "rider/src/main/kotlin/com/jetbrains/rider/model")

    // NOTE: classpath is evaluated lazily, at execution time, because it comes from the unzipped
    // intellij SDK, which is extracted in afterEvaluate
    params {
        verbose = true
        classpath { backend.getRiderModelJar() }
        sources "$modelDir/frontendBackend"
        hashFolder = "$hashBaseDir/frontendBackend"
        packages = "model.frontendBackend"

        // Standard JVM namespace for all built in models is com.jetbrains.rider.model
        generator {
            language = "kotlin"
            transform = "asis"
            namespace = "com.jetbrains.rider.model"
            directory = "$frontendKtOutput"
            root = "com.jetbrains.rider.model.nova.ide.IdeRoot"
        }

        // Standard C# namespace for all built in models is JetBrains.Rider.Model
        generator {
            language = "csharp"
            transform = "reversed"
            namespace = "JetBrains.Rider.Model"
            directory = "$backendCsOutput"
            root = "com.jetbrains.rider.model.nova.ide.IdeRoot"
        }
    }
}

task generateBackendUnityModel(type: tasks.getByName('rdgen').class) {
    def backendCsOutput = new File(repoRoot, "resharper/resharper-unity/src/Rider/Model")
    def unityEditorCsOutput = new File(repoRoot, "unity/EditorPlugin/Model")

    params {
        verbose = true
        classpath { backend.getRiderModelJar() }
        sources "$modelDir/backendUnity"
        hashFolder = "$hashBaseDir/backendUnity"
        packages = "model.backendUnity"

        // Standard C# namespace for all built in models is JetBrains.Rider.Model
        generator {
            language = "csharp"
            transform = "asis"
            namespace = "JetBrains.Rider.Model"
            directory = "$backendCsOutput"
            root = "model.backendUnity.BackendUnityModel"
        }

        // Standard C# namespace for all built in models is JetBrains.Rider.Model
        generator {
            language = "csharp"
            transform = "reversed"
            namespace = "JetBrains.Rider.Model"
            directory = "$unityEditorCsOutput"
            root = "model.backendUnity.BackendUnityModel"
        }
    }
}

task generateModels {
    group = 'protocol'
    description = 'Generates protocol models.'
    dependsOn generateFrontendBackendModel, generateBackendUnityModel
}

jar.dependsOn generateModels
// Make sure the dotnet build tasks depend on model, too
